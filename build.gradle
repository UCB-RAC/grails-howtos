apply plugin: "base"

version = 1.0

archivesBaseName = "grails-howtos"

ext.outputDir = new File(buildDir, "docs")
ext.gitDir = new File(buildDir, "wd")

tasks.addRule("Pattern: publish_<lang>") { String taskName ->
    def m = taskName =~ /publish_(\w+)/
    if (m) {
        def lang = m[0][1]
        task(taskName, type: grails.doc.gradle.PublishHowToTask) {
            language = lang
        }
    }
}

def langDirs = new File(projectDir, "src").listFiles({ f -> f.directory} as FileFilter)

task docs(type: Copy, dependsOn: langDirs.collect { project.tasks["publish_${it.name}"] }) {
    from file("resources/index.html")
    into outputDir
}

task dist(type: Zip, dependsOn: 'docs') {
    from outputDir
}

task cloneGhPages(type: grails.doc.gradle.GitCloneTask) {
    uri = "https://${-> project.getProperty("github.username")}@github.com/grails/grails-howtos.git"
    branch = "gh-pages"
    cloneAllBranches = true
    destination = gitDir
}
cloneGhPages.doFirst { assert gitDir.deleteDir() }

task copyHtml(type: Copy, dependsOn: [cloneGhPages, docs]) {
    from outputDir
    into gitDir
}

task gitAddFiles(type: grails.doc.gradle.GitAddTask, dependsOn: [copyHtml]) {
    repository = gitDir
    paths = ["."]
}

task gitCommit(type: grails.doc.gradle.GitCommitTask, dependsOn: [gitAddFiles]) {
    repository = gitDir
    message = "Automated publication of the HOWTO guides."
}

task gitPush(type: grails.doc.gradle.GitPushTask, dependsOn: [gitCommit]) {
    repository = gitDir
}

task deploy(dependsOn: [gitPush])

artifacts {
    archives dist
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.2'
}
